<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="utf-8">
  <title>Htmlayout 下拉组合框联动</title>
  <script type="text/javascript" src="../js/syntaxhighlighter.js"></script>
  <link type="text/css" rel="stylesheet" href="../css/monokai.css">
</head>
<body>
<script type="text/syntaxhighlighter" class="brush: aar"><![CDATA[
//下拉组合框联动
//${name: updataSelect}
//${title: Htmlayout 下拉组合框联动}

import win.ui;
/*DSG{{*/
var winform = win.form(text="下拉组合框联动";right=420;bottom=293)
winform.add()
/*}}*/

import console
import web.layout;
wbLayout = web.layout(winform);

wbLayout.html = /*
<meta charset="utf-8" />
<p>省份:<select #province size=1></select></p>
<p>城市:<select #city size=1><option>无选择</option></select></p>
*/

dataBind = function(id, data){
    var ele = wbLayout.queryEle(id).child(3); //select第3个子节点 <popup> 的子节点才是 <option> 节点
    var n = ele.childCount()
    for(i=n;1;-1) ele.child(i).delete()                                         //清空 <option> 节点
    for(i=1;#data) ele.printf("<option value='%d'>%s</option>", i, data[i])	    // 绑定data表
    wbLayout.queryEle(id).child(1).innerText = data[1]                          // 默认选中第1个选项
    ele.child(1).state.checked = true
}

dataBind( "#province", {"无选择";"江苏省";"浙江省";"广东省"} ) 

var city = {
    ["无选择"] = {"无选择"};
	["江苏省"] = {"南京市";"苏州市";"无锡市";"常州市"};
	["浙江省"] = {"杭州市";"宁波市";"温州市";"绍兴市"};
	["广东省"] = {"广州市";"深圳市";"珠海市";"东莞市"};
};
var pSelect = wbLayout.queryEle("#province")
pSelect.onSelectSelectionChanged = function (ltTarget,ltOwner,reason,behaviorParams) {
    var ltOption = ..web.layout.element( behaviorParams.he )
    dataBind("#city", city[ltOption.value])
}
pSelect.attachEventHandler()

winform.show();
win.loopMessage();

//-------------------------------------------
//select 实际渲染出来的节点是下面这个样子的
/*
<select id="city"  size="1"  type="select-dropdown" >
	<caption value="1" >南京市</caption>
	<button role="dropdown"  tabindex="-1" ></button>
	<popup type="select" >
        	<option value="1" >南京市</option>
        	<option value="2" >苏州市</option>
        	<option value="3" >无锡市</option>
        	<option value="4" >常州市</option>
	</popup>
</select>
*/
//-------------------------------------------

// https://suiang.cn/aardio/code/updataSelect.html
]]></script>
</body>
</html>
